pragma ever-solidity >= 0.62.0;
pragma AbiHeader time;
pragma AbiHeader expire;


contract Sample {
    uint static nonce;
    uint256 static owner;

    uint[] public state;

    event StateChange(uint _state);

    constructor(uint _state) public {
        tvm.accept();
        setState(_state);
    }

    struct Session {
        uint64 created;
    }
    mapping (uint256 => Session) public sessions;

    uint8 constant MAX_CLEANUP_MSGS = 30;
    mapping(uint256 => uint32) m_messages;

    function gc() private inline {
        uint counter = 0;
        for ((uint256 bodyHash, uint32 expireAt) : m_messages) {
            if (counter >= MAX_CLEANUP_MSGS) {
                break;
            }
            counter++;
            if (expireAt <= now) {
                delete m_messages[bodyHash];
            }
        }
    }

    function afterSignatureCheck(TvmSlice body, TvmCell) private inline returns (TvmSlice) {
        // load and drop message timestamp (uint64)
        (, uint32 expireAt) = body.decode(uint64, uint32);
        require(expireAt > now, 5700);
        uint256 bodyHash = tvm.hash(body);
        require(!m_messages.exists(bodyHash), 1002);
        tvm.accept();
        m_messages[bodyHash] = expireAt;
        return body;
    }

    function addSessionKeys(uint256[] hashes) public externalMsg {
        require(msg.pubkey() == owner, 101);
        tvm.accept();
        for (uint256 hash_ : hashes) {
            sessions[hash_] = Session({created: tx.timestamp});
        }
        gc();
    }

    modifier onlySessionKey(string key) {
        require(sessions.exists(sha256(key)), 200);
        tvm.accept();
        delete sessions[sha256(key)];
        tvm.commit();
        _;
    } 

    function setStateBySessionKey(string key, uint _state) public externalMsg onlySessionKey(key) {
        setState(_state);
        gc();
    }

    function setStateByOwner(uint _state) public externalMsg {
        require(msg.pubkey() == owner, 101);
        tvm.accept();
        setState(_state);
        gc();
    }

    function setStateByAnyone(uint _state) external internalMsg {
        require(msg.value >= 1_000_000_000, 102);
        setState(_state);
    }

    function setState(uint _state) private {
        require(_state <= 100, 103);
        state.push(_state);

        emit StateChange(_state);
    }

}
